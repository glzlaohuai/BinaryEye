apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

apply plugin: 'stringfog'
apply plugin: "com.bytedance.android.aabResGuard"

aabResGuard {
//    mappingFile = file("mapping.txt").toPath() // 用于增量混淆的 mapping 文件
    whiteList = [ // 白名单规则
                  "*.R.raw.*",
                  "*.R.drawable.icon"
    ]
    obfuscatedBundleFileName = "duplicated-app.aab" // 混淆后的文件名称，必须以 `.aab` 结尾
    mergeDuplicatedRes = true // 是否允许去除重复资源
    enableFilterFiles = true // 是否允许过滤文件
    filterList = [ // 文件过滤规则
                   "*/arm64-v8a/*",
                   "META-INF/*"
    ]
    enableFilterStrings = false // 过滤文案
}


stringfog {
    implementation 'com.github.megatronking.stringfog.xor.StringFogImpl'
    enable true
    fogPackages = ['com.awesomeapp.xscaner.widget']
}


android {
    namespace 'com.awesomeapp.xscaner'
    compileSdkVersion sdk_version
    defaultConfig {
        minSdkVersion 9
        targetSdkVersion sdk_version

        versionCode 120
        versionName '1.60.3'
    }

    signingConfigs {
        release {
            keyAlias System.getenv('ANDROID_KEY_ALIAS')
            keyPassword System.getenv('ANDROID_KEY_PASSWORD')
            storePassword System.getenv('ANDROID_STORE_PASSWORD')
            def filePath = System.getenv('ANDROID_KEYFILE')
            storeFile filePath ? file(filePath) : null
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }

        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
        }
    }

    bundle {
        language {
            // To make the app bundle contain all language resources
            // so the in-app language setting works. Another solution
            // would be to use the PlayCore API to download language
            // resources on demand, but it makes much more sense to
            // simply include the ~60 kb than adding fragile code and
            // dependencies.
            enableSplit = false
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:preference-v7:$support_version"
    implementation "com.android.support:preference-v14:$support_version"
    implementation 'com.github.markusfisch:CameraView:1.9.1'
    implementation 'com.github.markusfisch:ScalingImageView:1.4.1'
    implementation 'com.github.markusfisch:zxing-cpp:v2.1.0.0'

    implementation 'com.github.megatronking.stringfog:interface:3.0.0'
    implementation 'com.github.megatronking.stringfog:xor:3.0.0'


}
